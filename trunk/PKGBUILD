# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=bitwarden_rs-web
_pkgver=2.18.2
pkgver=$_pkgver
_upstreamver=$_pkgver
_patchver=2.18.1
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with bitwarden_rs'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('bitwarden_rs')
makedepends=('git' 'nodejs-lts-erbium' 'npm' 'python2')
provides=('bitwarden_rs-vault')
replaces=('bitwarden_rs-vault')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        'git+https://github.com/bitwarden/jslib.git'
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'SKIP'
        'b929394b653db0e13128dbf3a7ad6769dab32dfb9e7d0ec03dbcdf78f462226bbc19ddec98066a65bdf894204cc719ee7270152e1745fc38c136769c597a2d4c')

prepare() {
  cd bitwarden-web
  git submodule init
  git config submodule.jslib.url ../jslib
  git submodule update
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch
  npm ci
}

build() {
  cd bitwarden-web
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/$pkgname
  cp -R build/* "$pkgdir"/usr/share/$pkgname
}

# vim:set ts=2 sw=2 et:
